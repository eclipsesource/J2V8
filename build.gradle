buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.2'
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.eclipsesource.j2v8'
version = System.getenv('J2V8_FULL_VERSION') ?: '6.3.2'

base {
    archivesName = 'j2v8'
}

ext."signing.secretKeyRingFile" = '/.gnupg/secring.gpg'
ext."signing.password" = System.getenv("KEYSTORE_PASSWORD")
ext."signing.keyId" = System.getenv("KEY_ID")

repositories {
    google()
    mavenCentral()
}

android {
    namespace 'com.eclipsesource.v8'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 34
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
}

afterEvaluate {
    publishing {
        publications {
            create('release', MavenPublication) {
                from(components['release'])
                groupId = project.group
                artifactId = project.base.archivesName.get()
                version = project.version

                pom {
                    name = 'j2v8'
                    packaging = 'aar'
                    description = 'J2V8 is a set of Java bindings for V8'
                    url = 'https://github.com/eclipsesource/j2v8'

                    scm {
                        url = 'scm:git:git@github.com:eclipsesource/J2V8.git'
                        connection = 'scm:git:git@github.com:eclipsesource/J2V8.git'
                        developerConnection = 'scm:git:git@github.com:eclipsesource/J2V8.git'
                    }

                    licenses {
                        license {
                            name = 'Eclipse Public License - v 1.0'
                            url = 'https://www.eclipse.org/legal/epl-v10.html'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'irbull'
                            name = 'R. Ian Bull'
                            email = 'irbull@eclipsesource.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        if (findProperty('signing.keyId')) {
            sign publishing.publications.release
        }
    }
}

nexusPublishing {
    repositories {
        central {
            nexusUrl.set(uri("https://central.sonatype.com/api/v1/publisher/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/api/v1/publisher/"))
            username = System.getenv('MAVEN_REPO_USER')
            password = System.getenv('MAVEN_REPO_PASS')
        }
    }
}
